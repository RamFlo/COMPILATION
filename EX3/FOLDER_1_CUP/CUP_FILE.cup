/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;

parser code 
{:
	public Lexer lexer;
	
	private int curLine = 1, curCol = 0;
	
	private int lastDecLine = 1, lastDecCol = 0;
	
	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		//System.out.print(lexer.getLine());
		System.out.print(curLine);
		System.out.print(":");
		//System.out.print(lexer.getCharPos());
		System.out.print(curCol);
		System.out.print("] \n");
		System.out.print(message);
		//throw new ParserRuntimeException(String.format("ERROR(%d)\n",lexer.getLine()));
		throw new ParserRuntimeException(String.format("ERROR(%d)\n",curLine));
		//System.exit(0);
	}
	private void updateResultLocation(AST_Node RESULT)
	{
		RESULT.setLineAndColNum(curLine,curCol);
		curLine = lexer.getLine();
		curCol = lexer.getCharPos();
	}
	
	private void updateResultLocationLastDec(AST_Node RESULT)
	{
		RESULT.setLineAndColNum(lastDecLine,lastDecCol);
		curLine = lexer.getLine();
		curCol = lexer.getCharPos();
		
		lastDecLine = lexer.getLine();
		lastDecCol = lexer.getCharPos();
	}
	
	private void updateLocation()
	{
		curLine = lexer.getLine();
		curCol = lexer.getCharPos();
	}
	
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};
/*after reduce {:
	if (AST_Node.class.isInstance(RESULT))
	{
		AST_Node curAst = AST_Node.class.cast(RESULT);
		ast.setLineAndColNum(lexer.getLine(),lexer.getCharPos());
	}
:};*/

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal NEW;
terminal COMMA;
terminal EXTENDS;
terminal ARRAY;
terminal NIL;
terminal GT;
terminal LT;
terminal CLASS;
terminal RETURN;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_DEC_LIST	decList;
non terminal AST_DEC		dec;
non terminal AST_DEC_VAR	varDec;
non terminal AST_DEC_FUNC	funcDec;
non terminal AST_DEC_CLASS	classDec;
non terminal AST_DEC_ARRAY	arrayDec;
non terminal AST_EXP exp;
non terminal AST_STMT_LIST stmtList;
non terminal AST_STMT stmt;
non terminal AST_NEWEXP newExp;
non terminal AST_VAR var;
non terminal AST_EXP_CALL	callExp;
non terminal AST_EXP_LIST	expListComma;
non terminal AST_TYPE_NAME		typeName;
non terminal AST_TYPE_NAME_LIST	typeNameListComma;
non terminal AST_CFIELDLIST cFieldList;
non terminal Integer binop;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/************************/
/* START WITH: decList */
/************************/
start with decList;

/********************/
/* DERIVATION RULES */
/********************/


decList		::=		dec:d decList:l	{: RESULT = new AST_DEC_LIST(d,l); :}
					| dec:d			{: RESULT = new AST_DEC_LIST(d,null); :};

dec			::=		varDec:d		{: RESULT = d; updateLocation(); :}
					| funcDec:d		{: RESULT = d; updateLocation(); :}
					| classDec:d	{: RESULT = d; updateLocation(); :}
					| arrayDec:d 	{: RESULT = d; updateLocation(); :};
										
varDec		::=		ID:type ID:var SEMICOLON	            {: RESULT = new AST_DEC_VAR(type,var,null, null); :}
					| ID:type ID:var ASSIGN exp:e SEMICOLON {: RESULT = new AST_DEC_VAR(type,var,e, null); :}
					| ID:type ID:var ASSIGN newExp:e SEMICOLON {: RESULT = new AST_DEC_VAR(type,var,null,e); :};				

funcDec		::=		ID:ret ID:name LPAREN typeNameListComma:tnl RPAREN LBRACE stmtList:body RBRACE
					{: RESULT = new AST_DEC_FUNC(ret,name,tnl,body); :}
					| ID:ret ID:name LPAREN RPAREN LBRACE stmtList:body RBRACE
					{: RESULT = new AST_DEC_FUNC(ret,name,null,body); :};

classDec	::=		CLASS ID:name LBRACE cFieldList:l RBRACE {: RESULT = new AST_DEC_CLASS(name, null, l); :}
					| CLASS ID:name EXTENDS ID:superName LBRACE cFieldList:l RBRACE {: RESULT = new AST_DEC_CLASS(name, superName, l); :};

arrayDec	::=		ARRAY ID:name EQ ID:type LBRACK RBRACK {: RESULT = new AST_DEC_ARRAY(name, type); :};
				
exp			::=		INT:i			{: RESULT = new AST_EXP_INT(i); :}
					| MINUS INT:i	{: RESULT = new AST_EXP_INT(-i); :}
					| STRING:s		{: RESULT = new AST_EXP_STRING(s); :}
					| LPAREN exp:e RPAREN {:RESULT = e; updateLocation(); :}
					| NIL		    {: RESULT = new AST_EXP_NIL(); :}
					| var:v			{: RESULT = v; updateLocation(); :}
					| callExp:e		{: RESULT = e; updateLocation(); :}
					| exp:e1 binop:b exp:e2	{: RESULT = new AST_EXP_BINOP(e1,e2,b); :};
					

				
stmtList	::=		stmt:s stmtList:l	{: RESULT = new AST_STMT_LIST(s,l); :}
					| stmt:s			{: RESULT = new AST_STMT_LIST(s,null); :};
					
stmt		::=		varDec:v                                                    {: RESULT = new AST_STMT_DEC_VAR(v); :}
					| callExp:callExp SEMICOLON									{: RESULT = new AST_STMT_CALL(callExp); :}
					| RETURN exp:e SEMICOLON									{: RESULT = new AST_STMT_RETURN(e); :}
					| RETURN SEMICOLON											{: RESULT = new AST_STMT_RETURN(null);  :}
					| var:v ASSIGN exp:e SEMICOLON								{: RESULT = new AST_STMT_ASSIGN(v,e,null); :}
					| var:v ASSIGN newExp:e SEMICOLON							{: RESULT = new AST_STMT_ASSIGN(v,null,e); :}
					| IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(cond,body); RESULT.setLineAndColNum(condleft+1, condright);   :}
					| WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(cond,body); :};



newExp		::= 	NEW ID:type 						{: RESULT = new AST_NEWEXP(type,null); :}
					| NEW ID:type LBRACK exp:e RBRACK 	{: RESULT = new AST_NEWEXP(type,e); :};
					
var		::=			ID:name							{: RESULT = new AST_VAR_SIMPLE(name);  :}
					| var:v DOT ID:fieldName		{: RESULT = new AST_VAR_FIELD(v,fieldName); :} 
					| var:v LBRACK exp:e RBRACK		{: RESULT = new AST_VAR_SUBSCRIPT(v,e); :};
					

binop		::=		PLUS					{: RESULT = 0; updateLocation(); :}
					| MINUS					{: RESULT = 1; updateLocation(); :}
					| TIMES					{: RESULT = 2; updateLocation(); :}
					| DIVIDE				{: RESULT = 3; updateLocation(); :}
					| LT					{: RESULT = 4; updateLocation(); :}
					| GT					{: RESULT = 5; updateLocation(); :}
					| EQ					{: RESULT = 6; updateLocation(); :};
					 
					
callExp		::=		ID:funcName LPAREN expListComma:el RPAREN				{: RESULT = new AST_EXP_CALL(null,funcName,el);   :}
					| ID:funcName LPAREN RPAREN								{: RESULT = new AST_EXP_CALL(null,funcName,null); :}
					| var:v DOT ID:funcName LPAREN expListComma:el RPAREN	{: RESULT = new AST_EXP_CALL(v,funcName,el);   :}
					| var:v DOT ID:funcName LPAREN RPAREN					{: RESULT = new AST_EXP_CALL(v,funcName,null); :};

expListComma	::=		exp:e COMMA expListComma:el	{: RESULT = new AST_EXP_LIST(e,el); :}
						| exp:e						{: RESULT = new AST_EXP_LIST(e,null); :};

typeName				::=		ID:type ID:name									{: RESULT = new AST_TYPE_NAME(type,name); :};

typeNameListComma		::=		typeName:tn COMMA typeNameListComma:tnl			{: RESULT = new AST_TYPE_NAME_LIST(tn,tnl); :}
								| typeName:tn									{: RESULT = new AST_TYPE_NAME_LIST(tn,null); :};

/*cField					::=		varDec:v										{: RESULT = v; :}
								| funcDec:f										{: RESULT = f; :};*/

cFieldList				::=		varDec:v cFieldList:l 							{: RESULT = new AST_CFIELDLIST(v,l);:}
								| varDec:v										{: RESULT = new AST_CFIELDLIST(v,null); :}
								| funcDec:f cFieldList:l 						{: RESULT = new AST_CFIELDLIST(f,l); :}
								| funcDec:f										{: RESULT = new AST_CFIELDLIST(f,null); :};
